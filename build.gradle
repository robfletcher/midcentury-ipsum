buildscript {
	repositories {
		maven { url "http://oss.jfrog.org/repo" }
		mavenCentral()
		maven { url "http://dl.bintray.com/robfletcher/gradle-plugins" }
	}
	dependencies {
		classpath "org.ratpack-framework:ratpack-gradle:0.9.0-SNAPSHOT"
		classpath "org.gradle.plugins:gradle-compass:1.0.7"
	}
}

repositories {
	maven { url "http://oss.jfrog.org/repo" }
	mavenCentral()
	maven { url "http://repo.springsource.org/repo" }
}

apply plugin: "ratpack-groovy"
apply plugin: "compass"
apply plugin: "idea"

compass {
	cssDir = file("src/ratpack/public/css")
	sassDir = file("src/main/sass")
	imagesDir = file("src/ratpack/public/img")
	relativeAssets = true
	debugInfo = false
	environment = "production"
}

processResources.inputs.files compileSass
run.dependsOn watchSass
clean.dependsOn cleanCompileSass

idea {
	project {
		jdkName "1.6"
		languageLevel "1.6"
	}
}

dependencies {
	springloaded "org.springsource.springloaded:springloaded-core:1.1.1"
}

run {
	systemProperty "ratpack.reloadable", "true"
}

task wrapper(type: Wrapper) {
	gradleVersion = "1.7-rc-1"
}

installApp {
	from run.workingDir
}

startScripts {
	doLast {
		unixScript.text = unixScript.text.replace('DEFAULT_JVM_OPTS=""', 'DEFAULT_JVM_OPTS="-Dratpack.port=$PORT"')
	}
}

task stage(dependsOn: ['clean', 'installApp'], description: "Heroku uses this as default")